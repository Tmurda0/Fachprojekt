module info.scce.cinco.fp.compdsl.GenerateComponentDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import de.jabc.cinco.meta.core.utils.mwe2.CustomEcoreEMFGeneratorFragment

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "info.scce.cinco.fp.compdsl"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "info.scce.cinco.fp.compdsl.ComponentDsl"
			fileExtensions = "comp"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			emfGenerator = CustomEcoreEMFGeneratorFragment {
				// flag every 'name' attribute as ID attribute
				idAttribute = {
					attributeName = "name"
				}
			}
		}
	}
}
